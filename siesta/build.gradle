/*
 * Copyright (c) 2017 Cadenza United Kingdom Limited
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M3'
    }
}

group 'com.cadenzauk'
version '1.0.0-SNAPSHOT'

project.ext {
    springVersion = '4.3.6.RELEASE'
    junitPlatformVersion = '1.0.0-M4'
    junitJupiterVersion = '5.0.0-M4'
}

apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'org.junit.platform.gradle.plugin'

sourceSets {
    testutil {
        compileClasspath += sourceSets.main.runtimeClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }
    test {
        compileClasspath += sourceSets.testutil.runtimeClasspath
        runtimeClasspath += sourceSets.testutil.runtimeClasspath
    }
}

junitPlatform {
    platformVersion junitPlatformVersion
    filters {
        engines {
            include 'junit-jupiter', 'junit-vintage'
        }
    }
}

dependencies {
    compile group: 'org.jetbrains', name: 'annotations', version: '15.0'
    compile group: 'org.springframework', name: 'spring-context', version: springVersion
    compile group: 'org.springframework', name: 'spring-test', version: springVersion
    compile group: 'org.springframework', name: 'spring-jdbc', version: springVersion
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile group: 'org.hibernate.javax.persistence', name: 'hibernate-jpa-2.1-api', version: '1.0.0.Final'
    compile group: 'com.google.guava', name: 'guava', version: '21.0'
    compile group: 'cglib', name: 'cglib', version: '3.2.5'
    compile group: 'org.objenesis', name: 'objenesis', version: '2.5.1'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.9'

    testutilCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testutilCompile group: 'org.mockito', name: 'mockito-core', version: '2.7.17'
    testutilCompile group: 'org.liquibase', name: 'liquibase-core', version: '3.5.3'
    testutilCompile group: 'com.h2database', name: 'h2', version: '1.4.194'
    testutilCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion
    testutilCompile group: 'org.junit.platform', name: 'junit-platform-runner', version: junitPlatformVersion
    testutilCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitJupiterVersion

    testRuntime group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion
    testRuntime group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '4.12.0-M4'
}

task testutilJar(type: Jar) {
    classifier = 'testutil'
    from sourceSets.testutil.output
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
    archives testutilJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.hasProperty("ossrhUsername") ? ossrhUsername : "", password: project.hasProperty("ossrhPassword") ? ossrhPassword : "")
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: project.hasProperty("ossrhUsername") ? ossrhUsername : "", password: project.hasProperty("ossrhPassword") ? ossrhPassword : "")
            }

            pom.project {
                name 'Cadenza UK SIESTA'
                packaging 'jar'
                artifactId 'siesta'
                description 'SIESTA Is an Easy SQL Typesafe API'
                url 'https://github.com/cadenzauk/siesta'

                scm {
                    url 'scm:git@github.com:cadenzauk/siesta.git'
                    connection 'scm:git@github.com:cadenzauk/siesta.git'
                    developerConnection 'scm:git@github.com:cadenzauk/siesta.git'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://raw.githubusercontent.com/cadenzauk/siesta/master/LICENCE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'mdrodg'
                        name 'Mark Rodgers'
                    }
                }
            }
        }
    }
}
