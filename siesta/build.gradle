/*
 * Copyright (c) 2017 Cadenza United Kingdom Limited
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
plugins {
    id 'java-library'
    id 'java-test-fixtures'
    id 'maven-publish'
    id 'signing'
}

group = 'com.cadenzauk'
version = '0.9.1'

repositories {
    mavenCentral()
}

dependencies {
    api group: 'org.hibernate.javax.persistence', name: 'hibernate-jpa-2.1-api', version: '1.0.0.Final'
    api group: 'org.jetbrains', name: 'annotations', version: '15.0'
    api group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    api group: 'com.google.guava', name: 'guava', version: '32.1.3-jre'
    api group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'

    implementation group: 'org.objenesis', name: 'objenesis', version: '2.5.1'

    testFixturesApi(group: 'org.springframework', name: 'spring-context', version: springVersion) {
        exclude group: 'commons-logging'
    }
    testFixturesApi(group: 'org.springframework', name: 'spring-test', version: springVersion) {
        exclude group: 'commons-logging'
    }
    testFixturesApi(group: 'org.springframework', name: 'spring-jdbc', version: springVersion) {
        exclude group: 'commons-logging'
    }
    testFixturesApi group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testFixturesApi group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
    testFixturesApi group: 'org.mockito', name: 'mockito-junit-jupiter', version: mockitoVersion
    testFixturesApi group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion
    testFixturesApi group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitJupiterVersion
    testFixturesApi group: 'co.unruly', name: 'java-8-matchers', version: '1.5'
    testFixturesApi group: 'com.h2database', name: 'h2', version: h2Version
    testFixturesImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonDatabindVersion
    testFixturesImplementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: jacksonVersion

    testFixturesRuntimeOnly group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.25'
    testImplementation 'org.exparity:hamcrest-date:2.0.8'
    testImplementation group: 'org.hsqldb', name: 'hsqldb', version: hsqldbVersion
    testImplementation group: 'org.apache.derby', name: 'derby', version: derbyVersion
    testImplementation group: 'com.google.guava', name: 'guava-testlib', version: '31.1-jre'
    testImplementation group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.3.3'
    testRuntimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: '1.5.11'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion
}

test {
    useJUnitPlatform {
        excludeEngines 'junit-vintage'
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

components.java.withVariantsFromConfiguration(configurations.testFixturesApiElements) { skip() }
components.java.withVariantsFromConfiguration(configurations.testFixturesRuntimeElements) { skip() }

publishing {
    repositories {
        maven {
            name = "ossrh"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact(tasks["sourcesJar"])
            artifact(tasks["javadocJar"])

            pom {
                name = 'Cadenza UK SIESTA'
                artifactId = 'siesta'
                packaging = 'jar'
                description = 'SIESTA Is an Easy SQL Typesafe API'
                url = 'https://github.com/cadenzauk/siesta'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://raw.githubusercontent.com/cadenzauk/siesta/master/LICENCE'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'mdrodg'
                        name = 'Mark Rodgers'
                    }
                }
                scm {
                    url = 'scm:git@github.com:cadenzauk/siesta.git'
                    connection = 'scm:git@github.com:cadenzauk/siesta.git'
                    developerConnection = 'scm:git@github.com:cadenzauk/siesta.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
