import org.jetbrains.kotlin.gradle.dsl.JvmTarget

/*
 * Copyright (c) 2019 Cadenza United Kingdom Limited
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
    id 'maven-publish'
    id 'signing'
}

group = 'com.cadenzauk'
version siestaVersion

repositories {
    mavenCentral()
}

dependencies {
    api "com.cadenzauk:siesta:$siestaVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    testImplementation testFixtures("com.cadenzauk:siesta:$siestaVersion")
    testImplementation 'com.natpryce:hamkrest:1.8.0.1'
    testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonDatabindVersion
    testImplementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: jacksonVersion
    testImplementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: jacksonVersion
    testImplementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: jacksonVersion

    testRuntimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: '1.5.11'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion
}

test {
    useJUnitPlatform {
        excludeEngines 'junit-vintage'
    }
}

compileKotlin {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_17)
    }
}

compileTestKotlin {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_17)
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

publishing {
    repositories {
        maven {
            name = "ossrh"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact(tasks["sourcesJar"])
            artifact(tasks["javadocJar"])

            pom {
                name = 'Cadenza UK SIESTA Kotlin'
                artifactId = 'siesta-kotlin'
                packaging = 'jar'
                description = 'SIESTA Is an Easy SQL Typesafe API with Kotlin Support'
                url = 'https://github.com/cadenzauk/siesta'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://raw.githubusercontent.com/cadenzauk/siesta/master/LICENCE'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'mdrodg'
                        name = 'Mark Rodgers'
                    }
                }
                scm {
                    url = 'scm:git@github.com:cadenzauk/siesta.git'
                    connection = 'scm:git@github.com:cadenzauk/siesta.git'
                    developerConnection = 'scm:git@github.com:cadenzauk/siesta.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
